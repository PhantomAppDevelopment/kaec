<?xml version="1.0" encoding="utf-8"?>
<s:View xmlns:fx="http://ns.adobe.com/mxml/2009" 
		xmlns:s="library://ns.adobe.com/flex/spark" title="KAEC Videos" viewActivate="init(event)">
	<fx:Script>
		<![CDATA[
			import mx.collections.ArrayList;
			import mx.events.FlexEvent;
			import mx.events.PropertyChangeEvent;
			import mx.rpc.events.ResultEvent;
			
			import spark.events.IndexChangeEvent;
			import spark.events.ViewNavigatorEvent;
			
			private static const YOUTUBE_API_KEY:String = "COPY_YOUR_OWN_YOUTUBE_APIKEY";
			private var pageToken:String;
			public var recent:Boolean;
			public var loading:Boolean;
			public var months:Object = new Object();
			
			protected function init(event:ViewNavigatorEvent):void
			{
				months["01"] = "January";
				months["02"] = "February";
				months["03"] = "March";
				months["04"] = "April";
				months["05"] = "May";
				months["06"] = "June";
				months["07"] = "July";
				months["08"] = "August";
				months["09"] = "September";
				months["10"] = "October";
				months["11"] = "November";
				months["12"] = "December";
				
				myBusy.visible = true;
				pageToken = "";
				recent = true;
				recentVideos.url = "https://www.googleapis.com/youtube/v3/search?q=KAEC&maxResults=50&order=relevance&type=video&part=snippet&key="+YOUTUBE_API_KEY;
				recentVideos.send();
			}
			
			protected function changeState(state:String):void
			{
				currentState = state;
				
				if(state == "PopularState"){
					recent = false;
				} else {
					recent = true;
				}
				
				pageToken = "";
				myBusy.visible = true;
				
				if(recent == false){
					recentVideos.url = "https://www.googleapis.com/youtube/v3/search?q=KAEC&maxResults=50&order=relevance&type=video&part=snippet&key="+YOUTUBE_API_KEY;
				} else {
					recentVideos.url = "https://www.googleapis.com/youtube/v3/search?q=KAEC&maxResults=50&order=date&type=video&part=snippet&key="+YOUTUBE_API_KEY;
				}
				
				videosList.dataProvider = null;
				recentVideos.send();
			}
			
			protected function recentVideos_resultHandler(event:ResultEvent):void
			{
				var rawData:Object = JSON.parse(String(event.result));
				pageToken = rawData.nextPageToken;
				
				if(videosList.dataProvider == null){
					var videosArray:ArrayList = new ArrayList(rawData.items as Array);
					videosList.dataProvider = videosArray;
				} else {
					for each (var item:* in rawData.items){
						videosList.dataProvider.addItem(item);
					}
				}
				
				loading = false;
				myBusy.visible = false;
			}
			
			protected function playVideo(event:IndexChangeEvent):void
			{
				var myObject:Object = new Object();
				myObject.title = videosList.selectedItem.snippet.title;
				myObject.url = videosList.selectedItem.id.videoId;
				navigator.pushView(VideoDetails, myObject);
			}
						
			protected function videosList_creationCompleteHandler(event:FlexEvent):void
			{
				videosList.scroller.viewport.addEventListener(PropertyChangeEvent.PROPERTY_CHANGE, propertyChangeHandler);
			}
			
			protected function propertyChangeHandler( event : PropertyChangeEvent):void {
				if(event.property == "verticalScrollPosition"){
					if(event.newValue == ( event.currentTarget.measuredHeight - event.currentTarget.height)){
						loadMore();
					}
				}
			}
			
			public function loadMore():void{
				if(!loading){
					myBusy.visible = true;
					loading = true;
					
					if(recent == false){
						recentVideos.url = "https://www.googleapis.com/youtube/v3/search?q=KAEC&maxResults=50&pageToken="+pageToken+"&order=relevance&type=video&part=snippet&key="+YOUTUBE_API_KEY;
					} else {
						recentVideos.url = "https://www.googleapis.com/youtube/v3/search?q=KAEC&maxResults=50&pageToken="+pageToken+"&order=date&type=video&part=snippet&key="+YOUTUBE_API_KEY;
					}
					
					recentVideos.send();
				}
			}
			
		]]>
	</fx:Script>
	
	<fx:Declarations>
		<s:HTTPService id="recentVideos" result="recentVideos_resultHandler(event)"/>
	</fx:Declarations>
	
	<s:states>
		<s:State name="PopularState"/>
		<s:State name="RecentState"/>
	</s:states>
	
	<s:navigationContent>
		<s:Button click="navigator.popView()">
			<s:icon>
				<s:MultiDPIBitmapSource source160dpi="@Embed('assets/icons/160/1_navigation_previous_item.png')"
										source240dpi="@Embed('assets/icons/240/1_navigation_previous_item.png')"
										source320dpi="@Embed('assets/icons/320/1_navigation_previous_item.png')"/>
			</s:icon>
		</s:Button>
	</s:navigationContent>
	
	<s:HGroup id="menuGroup" height="50" top="0" width="100%">
		<s:Group click="changeState('PopularState')" width="50%" height="100%">
			<s:Label text="Popular" fontSize="16" styleName="myLabel" verticalCenter="0" horizontalCenter="0"/>
		</s:Group>
		<s:Group click="changeState('RecentState')" width="50%" height="100%">
			<s:Label text="Recent" fontSize="16" styleName="myLabel" verticalCenter="0" horizontalCenter="0"/>
		</s:Group>
	</s:HGroup>
	
	<s:Line left.PopularState="0" right.RecentState="0" y="50" width="50%">
		<s:stroke>
			<s:SolidColorStroke color="#FFFFFF" weight="3"/>
		</s:stroke>
	</s:Line>
	
	<s:List styleName="myListStyle" creationComplete="videosList_creationCompleteHandler(event)" id="videosList" change="playVideo(event)" width="100%" top="50" bottom="0">
		<s:itemRenderer>
			<fx:Component>
				<s:IconItemRenderer messageStyleName="myMessageStyle2" iconScaleMode="letterbox" iconWidth="67" iconHeight="50" iconFunction="myIconFunction" messageFunction="myMessageFunction" labelFunction="myLabelFunction">
					<fx:Script>
						<![CDATA[
							
							private function myIconFunction(item:Object):String{		
								return data.snippet.thumbnails.medium.url;							
							}
							
							private function myLabelFunction(item:Object):String{								
								return data.snippet.title;
							}
							
							private function myMessageFunction(item:Object):String{								
								return data.snippet.channelTitle+ "\n" + String(data.snippet.publishedAt).substr(0, 10);
							}
							
						]]>
					</fx:Script>
				</s:IconItemRenderer>
			</fx:Component>
		</s:itemRenderer>
	</s:List>
	
	<s:BusyIndicator id="myBusy" verticalCenter="0" horizontalCenter="0" visible="false"/>
	
</s:View>
